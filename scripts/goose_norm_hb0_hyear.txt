
data {
xm <- mean(x)
ym <- mean(y)
xsd <- sd(x)
ysd <- sd(y)
}

model {
  for (i in 1:Ntotal) {
    zy[i] ~ dnorm(zbeta0[s[i]] + zbeta1[s[i]] * zx[i],  
    1/zsigma[s[i]]^2)
  }
  
  # 'hyper' parameters
  #   for normal distributions that species intercepts & slopes come from
  for (j in 1:Nsubj) {
    zbeta0[j] ~ dnorm(zbeta0mu, 1/(zbeta0sigma)^2)
    zbeta1[j] ~ dnorm(zbeta1mu, 1/(zbeta1sigma)^2)
    zsigma[j] ~ dunif( 1.0E-3, 1.0E+3 )
  }
 
  # priors on standardized scale:
  zbeta0mu ~ dnorm(0, 1/(10)^2)
  zbeta1mu ~ dnorm(0, 1/(10)^2)
  zbeta0sigma ~ dunif( 1.0E-3, 1.0E+3 )
  zbeta1sigma ~ dunif( 1.0E-3, 1.0E+3 )
  
  # Tranform to original non-standardized scale
  for ( j in 1:Nsubj ) {
    beta1[j] <- zbeta1[j] * ysd / xsd
    beta0[j] <- zbeta0[j] * ysd + ym - zbeta1[j] * xm * ysd / xsd
    sigma[j] <- zsigma[j] * ysd
  }
  beta1mu <- zbeta1mu * ysd / xsd
  beta0mu <- zbeta0mu * ysd + ym - zbeta1mu * xm * ysd / xsd
  
}
